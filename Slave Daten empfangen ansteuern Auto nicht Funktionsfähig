/*
    ESP-NOW Broadcast Slave
    Lucas Saavedra Vaz - 2024

    This sketch demonstrates how to receive broadcast messages from a master device using the ESP-NOW protocol.

    The master device will broadcast a message every 5 seconds to all devices within the network.

    The slave devices will receive the broadcasted messages. If they are not from a known master, they will be registered as a new master
    using a callback function.
*/

#include "ESP32_NOW.h"
#include "WiFi.h"

#include <esp_mac.h>  // For the MAC2STR and MACSTR macros
#include <mcp_can.h>
#include <SPI.h>

#include <vector>

/* Definitions */

#define ESPNOW_WIFI_CHANNEL 11
#define CAN_CS_PIN 10  // CS-Pin f√ºr den CAN-Controller, bitte entsprechend deiner Hardware anpassen

/* Classes */
typedef struct message {
  char id[11];
  char message[128];

}message;

message received_message;

// Deklariere eine globale Instanz der MCP_CAN-Klasse
MCP_CAN CAN0(CAN_CS_PIN);  // Verwendet den angegebenen CS-Pin

// Creating a new class that inherits from the ESP_NOW_Peer class is required.

class ESP_NOW_Peer_Class : public ESP_NOW_Peer {
public:
  // Constructor of the class
  ESP_NOW_Peer_Class(const uint8_t *mac_addr, uint8_t channel, wifi_interface_t iface, const uint8_t *lmk) : ESP_NOW_Peer(mac_addr, channel, iface, lmk) {}

  // Destructor of the class
  ~ESP_NOW_Peer_Class() {}

  // Function to register the master peer
  bool add_peer() {
    if (!add()) {
      log_e("Failed to register the broadcast peer");
      return false;
    }
    return true;
  }

  // Function to print the received messages from the master
  void onReceive(const uint8_t *data, size_t len, bool broadcast) {
  Serial.printf("%d\n", len);
  if(len == sizeof(message)){
  memcpy(&received_message, data, sizeof(message));

  Serial.printf("Received a message from master " MACSTR " (%s)\n", MAC2STR(addr()), broadcast ? "broadcast" : "unicast");
  Serial.printf("  Message: %s\n", (char *)data);

  unsigned char Canbotschaft[8];
  memcpy(Canbotschaft, received_message.message, 8);  // Nur die ersten 8 Bytes kopieren

 unsigned long can_id = strtoul(received_message.id, NULL, 16);
  if (CAN0.sendMsgBuf(can_id, 0, 8, Canbotschaft) == CAN_OK) {
      Serial.println("Message sent successfully over CAN Bus");
  } else {
      Serial.println("Failed to send message over CAN Bus");
  }
  delay(5000);
  }}
};

/* Global Variables */

// List of all the masters. It will be populated when a new master is registered
std::vector<ESP_NOW_Peer_Class> masters;

/* Callbacks */

// Callback called when an unknown peer sends a message
void register_new_master(const esp_now_recv_info_t *info, const uint8_t *data, int len, void *arg) {
  if (memcmp(info->des_addr, ESP_NOW.BROADCAST_ADDR, 6) == 0) {
    Serial.printf("Unknown peer " MACSTR " sent a broadcast message\n", MAC2STR(info->src_addr));
    Serial.println("Registering the peer as a master");

    ESP_NOW_Peer_Class new_master(info->src_addr, ESPNOW_WIFI_CHANNEL, WIFI_IF_STA, NULL);

    masters.push_back(new_master);
    if (!masters.back().add_peer()) {
      Serial.printf("Failed to register the new master\n");
      return;
    }
  } else {
    // The slave will only receive broadcast messages
    log_v("Received a unicast message from " MACSTR, MAC2STR(info->src_addr));
    log_v("Igorning the message");
  }
}

/* Main */

void setup() {
  Serial.begin(115200);
  Serial.printf("Serial print beginnt\n");
  // Initialize the CAN Bus with the correct parameters
  
  while (!Serial) {
    delay(10);
  }

  if (CAN0.begin(MCP_NORMAL, CAN_250KBPS, MCP_8MHZ) == CAN_OK) {
    Serial.printf("CAN BUS Shield init OK!\n");
  } else {
    Serial.printf("CAN BUS Shield init FAIL\n");
    while (1);
  }
  
  // Initialize the Wi-Fi module
  WiFi.mode(WIFI_STA);
  WiFi.setChannel(ESPNOW_WIFI_CHANNEL);
  while (!WiFi.STA.started()) {
    delay(100);
  }

  Serial.println("ESP-NOW Example - Broadcast Slave");
  Serial.println("Wi-Fi parameters:");
  Serial.println("  Mode: STA");
  Serial.println("  MAC Address: " + WiFi.macAddress());
  Serial.printf("  Channel: %d\n", ESPNOW_WIFI_CHANNEL);

  // Initialize the ESP-NOW protocol
  if (!ESP_NOW.begin()) {
    Serial.println("Failed to initialize ESP-NOW");
    Serial.println("Reeboting in 5 seconds...");
    delay(5000);
    ESP.restart();
  }

  // Register the new peer callback
  ESP_NOW.onNewPeer(register_new_master, NULL);

  Serial.println("Setup complete. Waiting for a master to broadcast a message...");
}


void loop() {
  delay(1000);
}
